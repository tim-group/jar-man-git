plugins {
    id 'groovy'
    id "com.gradle.plugin-publish" version "1.2.0"
}

group = 'com.timgroup'
def buildNumber = providers.environmentVariable("BUILD_NUMBER")
if (buildNumber.present) version = "1.2.${buildNumber.get()}"
description = "Get scm info into the MANIFEST.MF and pom.xml"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation "org.eclipse.jgit:org.eclipse.jgit:4.5.0.201609210915-r"

    testImplementation "junit:junit:4.12"
}

gradlePlugin {
    website = "https://github.com/tim-group/jar-man-git"
    vcsUrl = "https://github.com/tim-group/jar-man-git"
    description = project.description
    plugins {
        create("jarmangit") {
            id = "com.timgroup.jarmangit"
            description = project.description
            tags.add("git")
            tags.add("pom")
            implementationClass = "com.timgroup.gradlejarmangit.GradleJarManGitPlugin"
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation "org.eclipse.jgit:org.eclipse.jgit:4.5.0.201609210915-r"

    implementation "junit:junit:4.13.1"
}

publishing {
    def repoUrl = providers.gradleProperty("repoUrl")
    def repoUsername = providers.gradleProperty("repoUsername")
    def repoPassword = providers.gradleProperty("repoPassword")
    def codeartifactUrl = providers.environmentVariable("CODEARTIFACT_URL")
            .orElse(providers.gradleProperty("codeartifact.url"))
            .orElse("https://timgroup-148217964156.d.codeartifact.eu-west-1.amazonaws.com/maven/jars/")
    def codeartifactToken = providers.environmentVariable("CODEARTIFACT_TOKEN")
            .orElse(providers.gradleProperty("codeartifact.token"))

    repositories {
        if (repoUrl.present && repoUsername.present && repoPassword.present) {
            maven {
                name = "nexus"
                url = "${repoUrl.get()}/repositories/yd-release-candidates"
                credentials {
                    username = repoUsername.get()
                    password = repoPassword.get()
                }
                allowInsecureProtocol = true
            }
        }
        if (codeartifactUrl.present && codeartifactToken.present) {
            maven {
                name = "codeartifact"
                url = codeartifactUrl.get()
                credentials {
                    username = "aws"
                    password = codeartifactToken.get()
                }
            }
        }
    }
}
