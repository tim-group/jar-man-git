plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.9.9"
}

group = 'com.timgroup'
if (System.getenv("BUILD_NUMBER")) version = '1.1.' + System.getenv('BUILD_NUMBER')
description = "Get scm info into the MANIFEST.MF and pom.xml"

repositories {
    mavenCentral()
}

gradlePlugin {
    plugins {
        jarmangit {
            id = "com.timgroup.jarmangit"
            implementationClass = "com.timgroup.gradlejarmangit.GradleJarManGitPlugin"
        }
    }
}

pluginBundle {
    website = "https://github.com/tim-group/jar-man-git"
    vcsUrl = "https://github.com/tim-group/jar-man-git"
    description = project.description
    plugins {
        jarmangit {
            id = "com.timgroup.jarmangit"
            displayName = project.description
            tags = ["git", "pom"]
        }
    }
}

publishing {
    repositories {
        if (project.hasProperty("repoUrl")) {
            maven {
                url "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
    }
}

model {
    publishing {
        publications {
            pluginMaven {
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar)

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

assemble.dependsOn(javadocJar)

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.eclipse.jgit:org.eclipse.jgit:4.5.0.201609210915-r"

    testCompile "junit:junit:4.12"
}
